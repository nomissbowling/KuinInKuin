const major: int :: 2019
const minor: int :: 12
const micro: int :: 22

func main()
	do refLibFuncs()
	
	if(!\option@acquireOption(lib@cmdLine()))
		do lib@exitCode(1)
		ret
	end if
	
	if(\option@version)
		do cui@print("Kuin Programming Language v.\{@major}.\{@minor}.\{@micro}\n")
		do cui@print("(C)Kuina-chan\n")
		do lib@exitCode(0)
		ret
	end if
	
	if(\option@help | \option@inputFile =& null)
		do cui@print("Usage: kuincl [-i input.kn] [-o output.kn] [-s 'sys' directory] [-e environment] [-a appcode] [-p 'res' directory] [-c icon.ico] [-r] [-h] [-v] [-q] [-x extended option]\n")
		do lib@exitCode(0)
		ret
	end if
	
	if(@build())
		do \err@err(%success, null, null)
		do lib@exitCode(0)
		ret
	end if
	
	do \err@err(%failure, null, null)
	do lib@exitCode(1)
	
	func refLibFuncs()
		; Prevent dead code elimination.
		if(env = "cpp")
			var refSetLogFunc: func<(func<(int, []char, []char, int, int)>)> :: \err@setLogFunc
			var refSetOption: func<([]char, []char, []char, []char, bool, []char)> :: \interpret@setOption
		end if
	end func
end func

func[__rwi]build(): bool
	var asts: dict<[]char, \ast@Ast>
	var entry: \ast@AstFunc
	
	do \err@errCnt :: 0
	var beginTime: int :: lib@sysTime()
	do \err@err(%compilationStarted, null, [((lib@sysTime() - beginTime) $ float / 1000.0).toStr()])
	
	; 'Parse'
	do asts :: \parse@parse(null, null, null, null)
	do \err@err(%parsingCompleted, null, [((lib@sysTime() - beginTime) $ float / 1000.0).toStr()])
	
	; 'Analyze'
	var funcAttrs: dict<[]char, dict<[]char, bool>>
	do entry :: \analyze@analyze(asts, &funcAttrs)
	do \err@err(%semanticAnalysisCompleted, null, [((lib@sysTime() - beginTime) $ float / 1000.0).toStr()])
	
	; Stop compilation if some errors occur.
	if(\err@errCnt > 0)
		ret false
	end if
	
	; 'Output'
	switch(\option@env_)
	case %exe
		if(env = "web")
			ret false
		else
			if(!\exe\output@output(entry, funcAttrs))
				ret false
			end if
		end if
	case %cpp
		if(!\cpp\output@output(entry, funcAttrs))
			ret false
		end if
	case %web
		if(!\web\output@output(entry))
			ret false
		end if
	default
		assert false
	end switch
	
	do \err@err(%generationProcessCompleted, null, [((lib@sysTime() - beginTime) $ float / 1000.0).toStr()])
	if(\err@errCnt > 0)
		ret false
	end if
	
	ret true
end func
